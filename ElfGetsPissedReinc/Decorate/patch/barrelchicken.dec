actor BarrelChicken_Samsara : SamsaraMonsterBeast replaces BarrelChicken
{
	Health 35
	Radius 6
	Height 4
	Mass 40
	Speed 8
	PainChance 200
	Monster
	-SOLID
	-COUNTKILL
	+LOOKALLAROUND
	+WINDTHRUST
	+DONTMORPH
	+FLOORCLIP
	SeeSound "chicken/pain"
	AttackSound "chicken/attack"
	PainSound "chicken/pain"
	DeathSound "chicken/death"
	ActiveSound "chicken/active"
	Obituary "$OB_CHICKEN"
	Tag "Chicken"
	States
	{
	Spawn:
		CHKN AB 10 A_LookEX
		Loop
	See:
		CHKN AB 3 A_Chase
		Loop
	Pain:
		CHKN D 5 A_Feathers
		CHKN C 5 A_Pain
		Goto See
	Pain.MightyBoot:
        TNT1 A 0 A_Feathers
        TNT1 A 0 A_ChangeFlag("NOPAIN",1)
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        CHKN D 2 A_FaceTarget
        TNT1 A 0 A_Recoil(12)
        CHKN C 16 A_Pain
        goto See
	Melee:
		TNT1 A 0 A_ChangeFlag("NOPAIN",0)
		CHKN A 8 A_FaceTarget
		CHKN C 10 A_CustomMeleeAttack(random[ChicAttack](1,2))
		Goto See
	Death:
        TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
        TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
		CHKN E 6 A_Scream
		CHKN F 6 A_Feathers
		CHKN G 6
		CHKN H 6 A_NoBlocking
		CHKN IJK 6
		CHKN L -1
		Stop
	Death.WolfenFlamethrower:
	Death.TotenkopfFire:
        TNT1 A 0 A_Scream
        TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
        TNT1 AAAAAAA 0 A_Feathers
        TNT1 A -1 A_SpawnItem("WolfensteinChickenLeg")
        stop
	}
}