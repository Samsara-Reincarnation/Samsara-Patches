Actor REKKRDeathRaven_Samsara : SamsaraMonsterDemonicBoss replaces SpiderMastermind 7
{
	Health 5500
	Speed 24
	Radius 40
	Height 110
	ReactionTime 16
	Mass 500
	+SHOOTABLE
	+DROPOFF
	+FLOAT
	+CORPSE
	-COUNTKILL
	+BOSS
	Monster
	SeeSound "spider/sight"
	AttackSound "weapons/shotgf"
	PainSound "pain/pain"
	DeathSound "spider/death"
	ActiveSound "demon/active"
	DropItem "SamsaraDropItemKeenDie"
	Tag "Death Raven"
	Bloodcolor "ff 31 31"
	States
	{
	Spawn:
		SPID AC 10 A_Look
		loop
	See:
		TNT1 A 0 A_GiveInventory("SamsaraBossSightTaunt")
	See2:
		SPID ABCDEFABCDE 4 A_Chase
		SPID G 12 Bright A_FaceTarget
		SPID H 5 Bright A_FatAttack1
		SPID G 5 Bright A_Chase
		SPID H 5 Bright A_FatAttack2
		SPID G 5 Bright A_Chase
		SPID H 5 Bright A_FatAttack3
		SPID G 5 Bright A_Chase
		SPID H 5 Bright A_FatAttack2
		goto See2
	Missile:
		SPID J 12 Bright A_FaceTarget
		SPID K 6 Bright A_BspiAttack
		goto See2
	Death:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffectForBosses",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
		SPID I 20 A_Scream
		SPID L 16 A_KeenDie
		SPID M 12
		SPID N 10
		SPID O 20
		SPID P 10
		SPID Q 9
		SPID R 12
		SPID STUVWXY 10
		SPID Z 40
		SPID Z -1 A_Fall
		stop
	}
}

Actor REKKRDeathRaven_Samsara2 : REKKRDeathRaven_Samsara replaces Cyberdemon 16 { +COUNTKILL }

Actor REKKRDeathRavenBall_Samsara : ArachnotronPlasma replaces ArachnotronPlasma
{
	Damage 20
	SeeSound ""
	DeathSound ""
	States
	{
	Spawn:
		BAL1 F 24 Bright A_Tracer
	Spawn2:
		BAL1 G 2 Bright A_CyberAttack
		BAL1 F 4 Bright A_Tracer
		goto Spawn2
	Death:
		BAL1 G 3 Bright
		BAL1 H 5 Bright
		BAL1 I 6 Bright
		stop
	}
}