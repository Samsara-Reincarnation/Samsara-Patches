ACTOR Blot_Samsara : SamsaraMonsterGhost replaces Blot
{
	obituary "%o was engulfed by a blot."
	health 100
	radius 24
	height 24
	mass 50
	speed 0
	seesound "Blot/Sight"
	deathsound "Blot/Death"
	damagefactor "blot", 0
	MONSTER
	+STANDSTILL
	+MISSILEMORE
	+MISSILEEVENMORE
	+DONTHURTSPECIES
	+NODAMAGETHRUST
	+BLOODLESSIMPACT
	+LOWGRAVITY
	+NOBLOOD
	Tag "Blot"
	States
	{
	Spawn:
		TNT1 A 10 A_Look
		loop
	See:
		EYES A 1 bright A_Chase
		EYES AAAAA 0 A_SpawnItemEx ("BlotSmoke", -2, random (0, -8), random (-4, 4), 0, random (-1, 1), random (-1, 1), 0, 160)
		loop
	Missile:
		EYES A 1 bright A_FaceTarget
		TNT1 AAAAA 0 A_SpawnItemEx ("BlotSmoke", -2, random (0, -8), random (-4, 4), 0, random (-1, 1), random (-1, 1), 0, 160)
		EYES A 1 bright ThrustThingZ (0, 12, 0, 0)
		TNT1 A 0 ThrustThing(angle*256/360, 8, 0, 0)
		TNT1 AAAAA 0 A_SpawnItemEx ("BlotSmoke", -2, random (0, -8), random (-4, 4), 0, random (-1, 1), random (-1, 1), 0, 160)
	MidLeap:
		EYES A 1 bright A_SpawnItem ("BlotDamage")
		TNT1 A 0 A_CheckFloor ("Land")
		TNT1 AAAAA 0 A_SpawnItemEx ("BlotSmoke", -2, random (0, -8), random (-4, 4), 0, random (-1, 1), random (-1, 1), 0, 160)
		loop
	Land:
		EYES A 1 bright A_Stop
		TNT1 AAAAA 0 A_SpawnItemEx ("BlotSmoke", -2, random (0, -8), random (-4, 4), 0, random (-1, 1), random (-1, 1), 0, 160)
		goto Missile
	Pain:
		EYES A 0
		Goto See
	Pain.DoomDamageTypeStunner:
		EYES A 2 A_ChangeFlag("NOPAIN",true)
		EYES A 300
		TNT1 A 0 A_ChangeFlag("NOPAIN",false)
		Goto See
	Pain.MightyBoot:
		TNT1 A 0 A_PlaySound("duke/kickhit",CHAN_BODY)
		EYES A 2 A_FaceTarget
		TNT1 A 0 A_Recoil(14)
		EYES A 16
		Goto See
	Death:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		TNT1 A 1 A_Stop
		TNT1 AAAAA 0 A_SpawnItemEx ("BlotSmoke", -2, random (0, -8), random (-4, 4), 0, random (-2, 2), random (-2, 2), 0, 160)
		TNT1 A 1 A_ScreamAndUnblock
		TNT1 AAAAA 0 A_SpawnItemEx ("BlotSmoke", -2, random (0, -8), random (-4, 4), 0, random (-3, 3), random (-3, 3), 0, 160)
		TNT1 A 1 A_Stop
		TNT1 AAAAA 0 A_SpawnItemEx ("BlotSmoke", -2, random (0, -8), random (-4, 4), 0, random (-4, 4), random (-4, 4), 0, 160)
		TNT1 A 1 A_Stop
		TNT1 AAAAA 0 A_SpawnItemEx ("BlotSmoke", -2, random (0, -8), random (-4, 4), 0, random (-5, 5), random (-5, 5), 0, 160)
		TNT1 A 1 A_Stop
		TNT1 AAAAA 0 A_SpawnItemEx ("BlotSmoke", -2, random (0, -8), random (-4, 4), 0, random (-6, 6), random (-6, 6), 0, 160)
		TNT1 A 1 A_Stop
		TNT1 AAAAA 0 A_SpawnItemEx ("BlotSmoke", -2, random (0, -8), random (-4, 4), 0, random (-7, 7), random (-7, 7), 0, 160)
		TNT1 A 1 A_Stop
		TNT1 AAAAA 0 A_SpawnItemEx ("BlotSmoke", -2, random (0, -8), random (-4, 4), 0, random (-8, 8), random (-8, 8), 0, 160)
		TNT1 A 1 A_Stop
		TNT1 AAAAA 0 A_SpawnItemEx ("BlotSmoke", -2, random (0, -8), random (-4, 4), 0, random (-9, 9), random (-9, 9), 0, 160)
		stop
	}
}