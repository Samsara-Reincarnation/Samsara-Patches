Actor Ghoul_Samsara : SamsaraMonsterGhost replaces Deh_Actor_150
{
	Health 50
	Speed 12
	Radius 16
	Height 40
	ReactionTime 8
	PainChance 128
	Mass 50
	Monster
	+SOLID
	+SHOOTABLE
	+COUNTKILL
	+FLOAT
	+NOGRAVITY
	+NOBLOOD
	SeeSound "monsters/ghoul/sight"
	ActiveSound "monsters/ghoul/active"
	PainSound "monsters/ghoul/pain"
	DeathSound "monsters/ghoul/death"
	Obituary "$ID24_OB_GHOUL"
	Tag "Ghoul"
	States
	{
	Spawn:
		GHUL AB 10 A_Look
		loop
	See:
		GHUL AABBCCBB 3 A_Chase
		loop
	Missile:
		TNT1 A 0 A_ChangeFlag("NOPAIN",0)
		GHUL DE 4 Bright A_FaceTarget
		GHUL F 4 Bright A_CustomMissile("GhoulBall_Samsara",24,0,0)
		GHUL G 4 Bright
		goto see
	Pain:
		GHUL I 3 Bright
		GHUL K 3 Bright A_Pain
		goto see
	Pain.MightyBoot:
		TNT1 A 0 A_PlaySound("duke/kickhit",CHAN_BODY)
		GHUL I 2 bright A_FaceTarget
		TNT1 A 0 A_Recoil(14)
		GHUL I 16 bright A_Pain
		Goto See
	Death:
		GHUL L 5 Bright ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		GHUL M 5 Bright A_Scream
		GHUL NO 5 Bright
		GHUL P 5 Bright A_Fall
		GHUL QR 5 Bright
		GHUL S -1
		stop
	}
}

actor GhoulBall_Samsara
{
	Damage 3
	Speed 15
	FastSpeed 20
	Radius 6
	Height 8
	Alpha 0.5
	Projectile
	+NOBLOCKMAP
	+NOGRAVITY
	+DROPOFF
	+MISSILE
	RenderStyle "Translucent"
	SeeSound "imp/attack"
	DeathSound "imp/shotx"
    DamageType "DoomGhoul"
	states
	{
		spawn:
			GBAL AB 4 Bright
			loop
		death:
			GBAL C 5 Bright
			APBX BCDE 5 Bright
			stop
	}
}