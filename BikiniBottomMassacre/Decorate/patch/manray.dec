ACTOR ManRay_Samsara : SamsaraMonsterGhost replaces ManRay
{
	SpawnID 252
	Health 20
	Radius 20
	Height 56
	Mass 100
	Speed 32
	PainChance 170
	Monster
	+FLOORCLIP
	+NOBLOOD
	SeeSound "wolfss/sight"
	PainSound "wolfss/pain"
	DeathSound "wolfss/death"
	ActiveSound "wolfss/active"
	AttackSound "wolfss/attack"
	HitObituary "%o was killed by Man Ray!"
	Obituary "%o was killed by Man Ray!"
	Tag "Man Ray"
	States
	{
	Spawn:
		SSWV AB 10 A_Look
		Loop
	See:
		SSWV AABBCCDD 3 A_Chase
		Loop
	Melee:
		SSWV E 10 A_FaceTarget
		SSWV F 10 A_FaceTarget
		SSWV G 4 Bright A_HeadAttack
	Missile:
		TNT1 A 0 A_ChangeFlag("NOPAIN",0)
		SSWV E 10 A_FaceTarget
		SSWV F 10 A_FaceTarget
		SSWV G 4 Bright A_HeadAttack
		SSWV F 6 A_FaceTarget
		SSWV G 4 Bright A_HeadAttack
		SSWV F 1 A_CPosRefire
		Goto Missile+2
	Pain:
		SSWV H 3
		SSWV H 3 A_Pain
		Goto See
	Pain.DoomDamageTypeStunner:
		SSWV H 2 A_ChangeFlag("NOPAIN",true)
		SSWV H 300 A_Pain
		TNT1 A 0 A_ChangeFlag("NOPAIN",false)
		Goto See
	Pain.MightyBoot:
		TNT1 A 0 A_ChangeFlag("NOPAIN",1)
		TNT1 A 0 A_PlaySound("duke/kickhit",CHAN_BODY)
		SSWV H 2 A_FaceTarget
		TNT1 A 0 A_Recoil(14)
		SSWV H 16 A_Pain
		Goto See
	Death:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		SSWV I 5
		SSWV J 5 A_Scream
		SSWV K 5 A_NoBlocking
		SSWV L 5
		SSWV M -1
		Stop
	Raise:
		SSWV A 0
		"####" "#" 0 A_JumpIfInventory("EnemyZorched",1,"ZorchRaise")
		SSWV MLKJI 5
		Goto See
	}
}