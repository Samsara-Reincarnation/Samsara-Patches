Actor Snotfolus_2_PB : Snotfolus_2 replaces Snotfolus_2
{
	-INVULNERABLE
	-NODAMAGE
	-NOPAIN
	-NOTIMEFREEZE
	States
	{
	Spawn:
		CYBR AB 10 A_Look
		loop
	}
}

Actor SuperCycloptis_2_PB : SuperCycloptis_2 replaces FlemoidusCycloptisCommonusV3_2 {}

Actor FlembraneV3_2_PB : SamsaraMonsterFlemoid replaces Flembrane
{
	Health 1000
	Radius 64
	Height 64
	Mass 0x7FFFFFFF //10000
	Speed 0
	Obituary "$OB_FLEMBRANE"
	PainChance 20
	PainChance "MightyBoot", 0
	PainChance "WTMelee", 0
	PainChance "WTMeleeIce", 0
	DamageFactor "Flem", 0.0
	DamageFactor "Slime", 0.0  //in chex3.exe the slam now has slime damagetype
	Monster
	MinMissileChance 60
	+MISSILEMORE
	+MISSILEEVENMORE
	+FLOORCLIP
	+DONTGIB
	+DONTRIP
	+NOTARGET
	+NOINFIGHTING
	+NOICEDEATH
	+NOTDMATCH
	SeeSound "baron/sight"
	PainSound "baron/pain"
	DeathSound "baron/death"
	ActiveSound "baron/active"
	Tag "Flembrane"
	BloodColor "6f ef 67"
	States
	{
	Spawn:
		BOSS AB 10 A_Look
		Loop
	See:
		BOSS AABBAABB 3 A_Chase
		Loop
	Melee:
	Missile:
		TNT1 A 0 A_Jump(128,"Missile2")
		BOSS EF 8 A_FaceTarget
		BOSS G 8 A_CustomComboAttack("MaximusBall2_CQ1", 32, random[headattack](1,8)*10, "baron/melee", "flem", false)
		Goto See2
	Missile2:
		BOSS EF 4 A_FaceTarget
		BOSS G 4 A_CustomComboAttack("SlimeGrenade_CQ1", 32, random[headattack](1,8)*10, "baron/melee", "flem", false)
		Goto See2
	Pain:
		TNT1 AAAA 0 A_SpawnDebris("FlemoidGib",1)
		FLMB V 2
		FLMB V 2 A_Pain
		Goto See
	Pain.Zorch:
	Pain.ZorchPistol:
	Pain.PhasingZorch:
	Pain.ZorchKamikaze:
		BOSS H 2
		BOSS H 2 A_Pain
		Goto See
	Death:
	XDeath:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_PlaySound("flemdie",CHAN_VOICE)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("FlemoidGib",random(-10,10),random(-55,55),random(16,56),random(-2,2),random(-2,2),random(5,25))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("FlemoidGib",random(-10,10),random(-55,55),random(16,56),random(-2,2),random(-2,2),random(5,20))
		FLMB WXYZ 5
		FLMB Z -1 A_BossDeath
		Stop
	Death.Zorch:
	Death.ZorchPistol:
	Death.PhasingZorch:
	Death.ZorchKamikaze:
		BOSS I 8
		BOSS J 8 A_Scream
		BOSS K 8
		BOSS L 8 A_NoBlocking
		BOSS MN 8
		BOSS O -1
		Stop
	}
}