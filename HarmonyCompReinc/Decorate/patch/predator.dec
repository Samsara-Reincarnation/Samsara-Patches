actor WalkingPredator_Samsara : SamsaraMonsterBeastBoss replaces Cyberdemon
{
	health 3000
	speed 16
	radius 40
	height 110
	reactiontime 8
	painchance 20
	mass 1000
	seesound "cyber/sight"
	painsound "skull/melee"
	deathsound "cyber/death"
	activesound "cyber/active"
	MONSTER
	+BOSS
	+FLOORCLIP
	+NORADIUSDMG
	+DONTMORPH
	+MISSILEMORE
    +NOICEDEATH
	BloodColor "e3 35 04"
	DamageFactor "CorvusCrossbow", 1.15
	DamageFactor "DragonClaw", 1.6
	DamageFactor "PhasingZorch", 1.15
	DamageFactor "DukeIce", 1.15
	DamageFactor "DoomPlasma", 1.15
	DamageFactor "MarathonPlasma", 1.15
	DamageFactor "CorvusHellstaff", 1.15
	DamageFactor "CorvusHellstaffTomed", 1.15
	DamageFactor "WolfenFlamethrower", 1.15
	DamageFactor "FireExplosive", 1.15
	PainChance "MightyBoot", 255
	Tag "Predator"
	states
	{
		Spawn:
			CYBR A 10 A_Look
			TROO B 10 A_Look
			loop
		See:
			CYBR AA 3 A_Chase
			TROO BBCC 3 A_Chase
			loop
		Pain:
			TROO D 3 A_Pain
			goto See
		Missile:
			CYBR Q 12 A_FaceTarget
			CYBR R 0 A_SkelWhoosh
			CYBR R 12 Bright A_CyberAttack
			CYBR E 12 A_FaceTarget
			CYBR E 0 A_SkelWhoosh
			CYBR F 12 Bright A_CyberAttack
			CYBR AA 3 A_Chase
			TROO BBCC 3 A_Chase
			goto See
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			SKEL M 7
			SKEL N 7 A_Scream
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffectForBosses",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)	
			SKEL O 7 A_Fall
			SKEL P 7
			SKEL Q -1
			Stop
		Death.Massacre:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			SKEL M 7
			SKEL N 7 A_Scream
			SKEL O 7 A_Fall
			SKEL P 7
			SKEL Q -1
			Stop
	}
}

actor Neutron_Samsara replaces Rocket
{
	health 1000
	speed 30
	radius 11
	height 8
	damage 20
	seesound ""
	deathsound "weapons/rocklx"
	PROJECTILE
	states
	{
		Spawn:
			PLSS AB 6 bright
			loop
		Death:
			MISL B 8 bright A_Explode
			MISL C 6 bright
			MISL D 4 bright
			BAL2 D 4 bright
			BAL2 E 4 bright
			stop
	}
}